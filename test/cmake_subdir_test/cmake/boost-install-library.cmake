# Copyright Claus Klein, 2025-2026
# Distributed under the Boost Software License, Version 1.0.
# See accompanying file LICENSE_1_0.txt or copy at http://www.boost.org/LICENSE_1_0.txt

cmake_minimum_required(VERSION 3.30)

include_guard(GLOBAL)

include(CMakePackageConfigHelpers)
include(GNUInstallDirs)

# boost_install_library
# =====================
#
# Installs a library (or set of targets) along with headers, C++ modules,
# and optional CMake package configuration files.
#
# Usage:
# ------
#   boost_install_library(<name>
#     TARGETS <target1> [<target2> ...]
#     [DEPENDENCIES <dependency1> [<dependency2> ...]]
#     [NAMESPACE <namespace>]
#     [EXPORT_NAME <export-name>]
#     [DESTINATION <install-prefix>]
#     [NO_VERSION_SUFFIX]
#   )
#
# Arguments:
# ----------
#
# name
#   Logical package name (e.g. "boost_any").
#   Used to derive config file names and cache variable prefixes.
#
# TARGETS (required)
#   List of CMake targets to install.
#
# DEPENDENCIES (optional)
#   Semicolon-separated list, one dependency per entry.
#   Each entry is a valid find_dependency() argument list.
#   NOTE: you must use the bracket form for quoting if not only a package name is used! CK
#   "[===[Boost::type_traits 1.0.0]===] [===[Boost::scope 0.0.1 EXACT]===] fmt"
#
# NAMESPACE (optional)
#   Namespace for exported targets.
#   Defaults to "Boost::".
#
# EXPORT_NAME (optional)
#   Name of the CMake export set.
#   Defaults to "<name>-targets".
#
# DESTINATION (optional)
#   The install destination for CXX_MODULES.
#   Defaults to ${CMAKE_INSTALL_LIBDIR}/cmake/${name}/modules.
#
# NO_VERSION_SUFFIX (optional)
#   option to disable the versioning of install destinations
#
# Brief
# -----
#
# This function installs the specified project TARGETS and its FILE_SET
# HEADERS to the default CMAKE install destination.
#
# It also handles the installation of the CMake config package files if
# needed.  If the given targets has FILE_SET CXX_MODULE, it will also
# installed to the given DESTINATION
#
# Cache variables:
# ----------------
#
# BOOST_INSTALL_CONFIG_FILE_PACKAGES
#   List of package names for which config files should be installed.
#
# <PREFIX>_INSTALL_CONFIG_FILE_PACKAGE
#   Per-package override to enable/disable config file installation.
#   <PREFIX> is the uppercased package name with dots replaced by underscores.
#
# Caveats
# -------
#
# **Only one `FILE_SET CXX_MODULES` is yet supported to install with this
# function!**
#
# **Only header files contained in a `PUBLIC FILE_SET TYPE HEADERS` will be
# install with this function!**

function(boost_install_library name)
    # ----------------------------
    # Argument parsing
    # ----------------------------
    set(options NO_VERSION_SUFFIX)
    set(oneValueArgs NAMESPACE EXPORT_NAME DESTINATION)
    set(multiValueArgs TARGETS DEPENDENCIES)

    cmake_parse_arguments(BOOST "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

    if(NOT BOOST_TARGETS)
        message(FATAL_ERROR "boost_install_library(${name}): TARGETS must be specified")
    endif()

    if(CMAKE_SKIP_INSTALL_RULES)
        message(
            WARNING
            "boost_install_library(${name}): not installing targets '${BOOST_TARGETS}' due to CMAKE_SKIP_INSTALL_RULES"
        )
        return()
    endif()

    # gersemi: off
    set(_version_suffix)
    set(_include_install_dir)
    set(_lib_install_dir)
    set(_bin_install_dir)
    # NOTE: If one of this variables is not set, the default DESTINATION is used! CK
    if(NOT BOOST_NO_VERSION_SUFFIX)
        set(_version_suffix "-${PROJECT_VERSION}")
        set(_include_install_dir DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/boost${_version_suffix})
        # set(_lib_install_dir DESTINATION ${CMAKE_INSTALL_LIBDIR}/boost${_version_suffix})
        # set(_bin_install_dir DESTINATION ${CMAKE_INSTALL_BINDIR}/boost${_version_suffix})
    endif()
    set(_config_install_dir "${CMAKE_INSTALL_LIBDIR}/cmake/${name}${_version_suffix}")
    # gersemi: on

    # ----------------------------
    # Defaults
    # ----------------------------
    if(NOT BOOST_NAMESPACE)
        set(BOOST_NAMESPACE "Boost::")
    endif()

    if(NOT BOOST_EXPORT_NAME)
        set(BOOST_EXPORT_NAME "${name}-targets")
    endif()

    if(NOT BOOST_DESTINATION)
        set(BOOST_DESTINATION "${_config_install_dir}")
    endif()

    # XXX string(REPLACE "boost_" "" install_component_name "${name}")
    set(install_component_name "boost")
    message(VERBOSE "boost-install-library(${name}): COMPONENT '${install_component_name}'")

    # Quickfix for multible dependent components a la Boost
    set(_PROJECT_NAME "${name}")

    # --------------------------------------------------
    # Install each target with all of its file sets
    # --------------------------------------------------
    foreach(_tgt IN LISTS BOOST_TARGETS)
        if(NOT TARGET "${_tgt}")
            message(WARNING "boost_install_library(${name}): '${_tgt}' is not a target")
            continue()
        endif()

        # Given boost_any, the component_name is any
        string(REPLACE "_" ";" name_parts "${_tgt}")
        # fail if the name doesn't look like boost_any
        list(LENGTH name_parts name_parts_length)
        if(NOT name_parts_length GREATER_EQUAL 2)
            message(
                FATAL_ERROR
                "boost_install_library(${name}): expects a name of the form 'boost_<name>', got '${_tgt}'"
            )
        endif()
        # XXX list(GET name_parts -1 component_name)
        # TODO(CK) set(component_name "${_tgt}")
        string(REPLACE "boost_" "" component_name "${_tgt}")

        set_target_properties("${_tgt}" PROPERTIES EXPORT_NAME "${component_name}")
        message(
            VERBOSE
            "boost_install_library(${name}): EXPORT_NAME ${component_name} for TARGET '${_tgt}'"
        )

        # Get the list of interface header sets, exact one expected!
        set(_install_header_set_args)
        get_target_property(_available_header_sets ${_tgt} INTERFACE_HEADER_SETS)
        if(_available_header_sets)
            message(
                VERBOSE
                "boost-install-library(${name}): '${_tgt}' has INTERFACE_HEADER_SETS=${_available_header_sets}"
            )
            foreach(_install_header_set IN LISTS _available_header_sets)
                list(
                    APPEND _install_header_set_args
                    FILE_SET
                    "${_install_header_set}"
                    ${_include_install_dir}
                    COMPONENT
                    "${install_component_name}_Development"
                )
            endforeach()
        else()
            set(_install_header_set_args FILE_SET HEADERS) # NOTE: empty FILE_SET in this case! CK
        endif()

        # Detect presence of PUBLIC C++ module file sets, exact one expected!
        get_target_property(_module_sets "${_tgt}" INTERFACE_CXX_MODULE_SETS)
        if(_module_sets)
            message(
                VERBOSE
                "boost-install-library(${name}): '${_tgt}' has INTERFACE_CXX_MODULE_SETS=${_module_sets}"
            )
            install(
                TARGETS "${_tgt}"
                EXPORT ${BOOST_EXPORT_NAME}
                ARCHIVE ${_lib_install_dir} COMPONENT "${install_component_name}_Development"
                LIBRARY
                    ${_lib_install_dir}
                    COMPONENT "${install_component_name}_Runtime"
                    NAMELINK_COMPONENT "${install_component_name}_Development"
                RUNTIME ${_bin_install_dir} COMPONENT "${install_component_name}_Runtime"
                ${_install_header_set_args}
                FILE_SET ${_module_sets}
                    DESTINATION "${BOOST_DESTINATION}"
                    COMPONENT "${install_component_name}_Development"
                # NOTE: There's currently no convention for this location! CK
                CXX_MODULES_BMI
                    DESTINATION ${_config_install_dir}/bmi-${CMAKE_CXX_COMPILER_ID}_$<CONFIG>
                    COMPONENT "${install_component_name}_Development"
            )
        else()
            install(
                TARGETS "${_tgt}"
                EXPORT ${BOOST_EXPORT_NAME}
                ARCHIVE ${_lib_install_dir} COMPONENT "${install_component_name}_Development"
                LIBRARY
                    ${_lib_install_dir}
                    COMPONENT "${install_component_name}_Runtime"
                    NAMELINK_COMPONENT "${install_component_name}_Development"
                RUNTIME ${_bin_install_dir} COMPONENT "${install_component_name}_Runtime"
                ${_install_header_set_args}
            )
        endif()
    endforeach()

    # --------------------------------------------------
    # Export targets
    # --------------------------------------------------
    # gersemi: off
    install(
        EXPORT ${BOOST_EXPORT_NAME}
        NAMESPACE ${BOOST_NAMESPACE}
        CXX_MODULES_DIRECTORY cxx-modules
        DESTINATION ${_config_install_dir}
        COMPONENT "${install_component_name}_Development"
    )
    # gersemi: on

    # ----------------------------------------
    # Config file installation logic
    # ----------------------------------------
    string(TOUPPER "${name}" _pkg_upper)
    string(REPLACE "." "_" _pkg_prefix "${_pkg_upper}")

    option(
        ${_pkg_prefix}_INSTALL_CONFIG_FILE_PACKAGE
        "Enable creating and installing a CMake config-file package. Default: ON. Values: { ON, OFF }."
        ON
    )

    set(_pkg_var "${_pkg_prefix}_INSTALL_CONFIG_FILE_PACKAGE")

    if(NOT DEFINED ${_pkg_var})
        set(${_pkg_var} OFF CACHE BOOL "Install CMake package config files for ${name}")
    endif()

    set(_install_config OFF)

    if(${_pkg_var})
        set(_install_config ON)
    elseif(BOOST_INSTALL_CONFIG_FILE_PACKAGES)
        list(FIND BOOST_INSTALL_CONFIG_FILE_PACKAGES "${name}" _idx)
        if(NOT _idx EQUAL -1)
            set(_install_config ON)
        endif()
    endif()

    # ----------------------------------------
    # expand dependencies
    # ----------------------------------------
    set(_boost_find_deps "")
    foreach(dep IN ITEMS ${BOOST_DEPENDENCIES})
        message(VERBOSE "boost-install-library(${name}): Add find_dependency(${dep})")
        string(APPEND _boost_find_deps "find_dependency(${dep})\n")
    endforeach()
    set(BOOST_FIND_DEPENDENCIES "${_boost_find_deps}")

    # ----------------------------------------
    # Generate + install config files
    # ----------------------------------------
    if(_install_config)
        configure_package_config_file(
            "${CMAKE_CURRENT_FUNCTION_LIST_DIR}/Config.cmake.in"
            "${CMAKE_CURRENT_BINARY_DIR}/${name}-config.cmake"
            INSTALL_DESTINATION ${_config_install_dir}
        )

        write_basic_package_version_file(
            "${CMAKE_CURRENT_BINARY_DIR}/${name}-config-version.cmake"
            VERSION ${PROJECT_VERSION}
            COMPATIBILITY SameMajorVersion
        )

        install(
            FILES
                "${CMAKE_CURRENT_BINARY_DIR}/${name}-config.cmake"
                "${CMAKE_CURRENT_BINARY_DIR}/${name}-config-version.cmake"
            DESTINATION ${_config_install_dir}
            COMPONENT "${install_component_name}_Development"
        )
    else()
        message(
            WARNING
            "boost-install-library(${name}): Not installing a config package for '${name}'"
        )
    endif()
endfunction()

set(CPACK_GENERATOR TGZ)
include(CPack)
